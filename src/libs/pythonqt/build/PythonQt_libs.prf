# profile for non-mevis users to link to PythonQt

include ( python.prf )  

INCLUDEPATH += $$PWD/../src
DEPENDPATH += $$PWD/../src

# check if debug or release
CONFIG(debug, debug|release) {
  DEBUG_EXT = _d 
} else {
  DEBUG_EXT = 
}
win32-g++: {
pythonqt_copy.files = \
    $$DESTDIR/../../../build/src/libs/pythonqt/lib/PythonQt-Qt5-Python$${PYTHON_VERSION}$${DEBUG_EXT}.dll
}else:win32-msvc*: {
pythonqt_copy.files = \
    $$DESTDIR/../../../build/src/libs/pythonqt/lib/PythonQt-Qt5-Python$${PYTHON_VERSION}$${DEBUG_EXT}.lib
}else:unix: {
pythonqt_copy.files = \
    $$DESTDIR/../../../build/src/libs/pythonqt/lib/libPythonQt-Qt5-Python$${PYTHON_VERSION}$${DEBUG_EXT}.so
}

pythonqt_copy.path = $$DESTDIR

COPIES += pythonqt_copy

LIBS += -L$$DESTDIR/ -lPythonQt-Qt5-Python$${PYTHON_VERSION}$${DEBUG_EXT}
